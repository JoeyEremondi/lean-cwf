import CwF.Fam
import CwF.CwF
import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Functor.Basic
import Mathlib.CategoryTheory.Functor.Basic
import Mathlib.CategoryTheory.Comma.Over
import Mathlib.CategoryTheory.Comma.Basic
import Mathlib.Data.Opposite
import Mathlib.CategoryTheory.Limits.Shapes.Terminal
import Mathlib.Logic.Unique

open CategoryTheory

universe u v u2
section
  variable {C : Type u} [Category.{v}  C] [TmTy.{u,v} C] [cwf: CwF C]



  -- If you compose with an extension, this is the same as extending by the composition,
  -- except that you also end up substituting in the term you're extending by.
  -- Unfortunate ugliness due to the fact that Tmâ¦ƒg â‰« fâ¦„ is not definitionally equal to tmâ¦ƒfâ¦„â¦ƒgâ¦„
  @[simp]
  theorem ext_nat {Î“ Î” Î : C} {T : Ty Î“}
    (f : Î” âŸ¶ Î“)
    (g : Î âŸ¶ Î”)
    (t : Tm (Tâ¦ƒfâ¦„))
    : (g â‰« âŸªf , tâŸ«) = âŸªg â‰« f , (â†‘â‚œ tâ¦ƒgâ¦„) âŸ« := by
      fapply CwF.ext_unique <;> simp_all
      have eq2 := castSymm (tmSubComp (f := âŸªf , tâŸ«) (g := g) (t := CwF.v))
      rw [eq2]
      simp_all


  -- If you take a weaning and extend it with the newly introduced variable, you get the identity,
  -- because it just replaces each v with v
  @[simp]
  theorem ext_id {Î“ : C} {T : Ty Î“} : âŸªCwF.p , CwF.vâŸ« = ğŸ™ (Î“ â–¹ T) := by
    symm
    fapply CwF.ext_unique <;> simp_all

  -- Helper function for dependent cong
  -- Should really be in the stdlib
  -- TODO PR?
  theorem hCong {A : Type u} {B : A â†’ Type v} {f g : (a : A) â†’ B a} {x y : A}
    (funEq : f = g) (argEq : x = y) :
      HEq (f x) (g y) := by aesop

  theorem castCong {A : Type u} {B : A â†’ Type v} {f g : (a : A) â†’ B a} {x y : A}
    (funEq : f = g) (argEq : x = y) :
      (f x) = cast (by aesop) (g y) := by
      aesop


  theorem ext_inj {Î“ Î” : C} {Î¸â‚ Î¸â‚‚ : Î” âŸ¶ Î“} {T : Ty Î“} {tâ‚ : Tm (Tâ¦ƒÎ¸â‚â¦„)} {tâ‚‚ : Tm (Tâ¦ƒÎ¸â‚‚â¦„)}
    :
    (âŸªÎ¸â‚,tâ‚âŸ« = âŸªÎ¸â‚‚,tâ‚‚âŸ«) â†” (âˆƒ x : (Î¸â‚ = Î¸â‚‚), tâ‚ =â‚œ tâ‚‚) := by
      constructor <;> intro eq <;> try aesop_cat
      have peq := congrArg (Î» x => x â‰« CwF.p) eq
      have veq := castCong (refl (Î» x => CwF.v â¦ƒxâ¦„)) eq
      simp at peq
      aesop




---- Terms and Sections
-- There is an equivalence between terms of Tm T
-- and sections p_T

  -- Turn a term into the substitution that replaces v with that term
  abbrev toSub {Î“ : C} {T : Ty Î“} (t : Tm T) : Î“ âŸ¶ (Î“ â–¹ T) :=
    âŸª ğŸ™ _ , â†‘â‚œ t âŸ«

  -- That subsitution is a section of p
  abbrev toSection {Î“ : C} {T : Ty Î“} (t : Tm T) : SplitEpi (CwF.p (T := T)) :=
    âŸ¨ toSub t , by simp_all âŸ©

  -- Get a term out of any section of p
  abbrev toTerm {Î“ : C} {T : Ty Î“} (epi : SplitEpi (CwF.p (T := T))) : Tm T :=
    â†‘â‚œ ((CwF.v ) â¦ƒ epi.section_ â¦„)

  theorem congrDepâ‚‚  {A : Type } {B : A â†’ Type} {R :  Type} (f : (a : A) â†’ (b : B a) â†’ R)
    {aâ‚ aâ‚‚ : A} (eqa : aâ‚ = aâ‚‚) {bâ‚ : B aâ‚} {bâ‚‚ : B aâ‚‚} (eqb : bâ‚ = cast (by aesop) bâ‚‚)
    : f aâ‚ bâ‚ = (f aâ‚‚ bâ‚‚) := by
    cases eqa with
    | refl =>
      simp at eqb
      cases eqb with
        | refl => simp


  theorem extEq {Î“ Î” : C} {T : Ty Î“ } {f g : Î” âŸ¶ Î“ } {t : Tm (Tâ¦ƒfâ¦„)}
    (eq : f = g) : âŸªf , t âŸ« = âŸª g , castTmSub t eqâŸ« := by aesop


  theorem toSectionTerm {Î“ : C} {T : Ty Î“} (epi : SplitEpi (CwF.p (T := T))) : toSection (toTerm epi) = epi := by
    simp [toTerm, toSection, toSub]
    cases (epi) with
    | mk f eq =>
      congr
      simp_all
      rw [extEq (symm eq)]
      simp
      rw [<- ext_nat]
      simp

  theorem toTermSection {Î“ : C} {T : Ty Î“} (t : Tm T) : toTerm (toSection t) = t := by
    simp [toTerm, toSection, toSub]


  -- Terms and sections are equivalent
  theorem termSecEquiv {Î“ : C} {T : Ty Î“} : Function.Bijective (toSection (T := T))  := by
    constructor
    . apply Function.LeftInverse.injective (g := toTerm)
      apply toTermSection
    . apply Function.RightInverse.surjective (g := toTerm)
      apply toSectionTerm

  -- Corollary is that toTerm is injective: each unique section carves out a unique term
  -- which is useful when defining new terms by composing section
  -- especially for democratic CwFs
  theorem toTermInj {Î“ : C} {T : Ty Î“} : Function.Injective (toTerm (T := T)) := by
    apply Function.LeftInverse.injective
    apply toSectionTerm

  notation:10000 t "â»" => toSub t

  -- Weakening
  -- Lifts any substitution to work on an extended context
  abbrev wk {Î“ Î” : C} (f : Î” âŸ¶ Î“) {T : Ty Î“} : (Î” â–¹ Tâ¦ƒfâ¦„) âŸ¶ (Î“ â–¹ T) :=
    âŸªCwF.p (T := Tâ¦ƒfâ¦„) â‰« f , â†‘â‚œ CwF.v âŸ«

  -- Weakening morphisms are the CwF version of a substitution Î“(x:T)Î” âŸ¶ Î“ Î”
  -- i.e. as a substitution, we can introduce an unused variable anywhere in the context
  class Weakening (Î” Î“ : C) : Type _ where
    weaken : Î” âŸ¶ Î“

  instance wkBase {Î“ : C} {T : Ty Î“} : Weakening (Î“ â–¹ T) Î“ where
    weaken := CwF.p

  instance wkStep {Î” Î“ : C} [inst : Weakening Î” Î“] {T : Ty Î“}  : Weakening (Î” â–¹ Tâ¦ƒinst.weakenâ¦„) (Î“ â–¹ T) where
    weaken := wk (inst.weaken) (T := T)

  notation:10000 T "âº" => tySub T Weakening.weaken
  notation:10000 t "âº" => tmSub t Weakening.weaken


  -- All equalities between terms can be deduced from equalities between morphisms
  theorem eqAsSections {Î“ : C} {T : Ty Î“} {t1 t2 : Tm T} (eq :  t1â» =  t2â»)
    : t1 = t2 := by
    apply Function.LeftInverse.injective (toTermSection)
    simp_all



  @[simp]
  theorem vCast {Î“  : C} {T : Ty Î“} {f : _} (eq : f = ğŸ™ (Î“ â–¹ T)) : (tmSub (CwF.v (T := T)) f)  =â‚œ CwF.v := by
    aesop

  @[simp]
  theorem wkTm {Î“ Î” : C} (Î¸ : Î” âŸ¶ Î“) {T : Ty Î“} {t : Tm T}
  : (tâ¦ƒÎ¸â¦„)â» â‰« (wk Î¸) = Î¸ â‰« (tâ») := by
    simp [toSub]
    rw [ext_inj]
    apply Exists.intro <;> simp_all
    simp [<- Category.assoc]

end

--Given the functoral definition of substitution on terms and types for a category of contexts,
--context extension is unique up to isomorphism
lemma cwfUnique {C : Type u} [Category.{v}  C] [TmTy.{u,v} C] [Limits.HasTerminal C]
  (inst1 : CwF C) (inst2 : CwF C) {Î“ : C} {T : Ty Î“} :  (inst1.snoc Î“ T)  â‰…  (inst2.snoc Î“ T) where
  -- Bascially a dependent version of the uniqueness of products
  hom := inst2.ext (inst1.p (T := T)) inst1.v
  inv := inst1.ext (inst2.p (T := T)) inst2.v
  hom_inv_id := by
    rw [<- ext_id (cwf := inst1) (T := T)]
    fapply inst1.ext_unique
      <;> try simp [ext_nat (cwf := inst1), inst1.ext_p ]
    trans
    . apply castSymm
      apply tmSubComp
    . simp_rw [inst1.ext_v]
      simp only [castSub, inst2.ext_v, cast_cast]
  inv_hom_id := by
    rw [<- ext_id (cwf := inst2) (T := T)]
    fapply inst2.ext_unique <;> try simp [ext_nat (cwf := inst1), inst1.ext_p]
    trans
    . apply castSymm
      apply tmSubComp
    simp_rw [inst2.ext_v]
    simp only [castSub, inst1.ext_v, cast_cast]



----------------------------------------------------------
-- Some useful tools for going between morphisms and terms

section

  variable {C : Type u} [Category.{v}  C] [TmTy.{u,v} C]  [cwf: CwF C]


  -- These lemmas encode a generalization of the "terms as sections of display maps"
  -- idea, where germs in an indexed type correspond to arrows in the slice category
  -- between the specific index values and a display map.
  -- When you plug in id for the arrow, you get terms as sections

  abbrev tyToSlice {Î“ : C} (T : Ty Î“) : Over Î“ :=
    Over.mk (CwF.p (T := T))

  def secToSliceArrow {Î“ : C} {T : Ty Î“} (sec : SplitEpi (CwF.p (T := T)))
    : (Over.mk (ğŸ™ Î“) âŸ¶ tyToSlice T) :=
      Over.homMk (SplitEpi.section_ sec)

  def sliceArrowToSection {Î“ : C} {T : Ty Î“} (sliceArr : Over.mk (ğŸ™ Î“) âŸ¶ tyToSlice T)
    : SplitEpi (CwF.p (T := T)) := SplitEpi.mk (sliceArr.left)
      (by have pf := Over.w sliceArr
          simp_all [tyToSlice]
          )


  def extHead {Î“ Î” : C} {T : Ty Î“} (f : Î” âŸ¶ Î“ â–¹ T) : Tm (Tâ¦ƒf â‰« CwF.pâ¦„) :=
    â†‘â‚œ CwF.vâ¦ƒfâ¦„

  theorem headTmEq {Î“ Î” : C} {T : Ty Î“} (f : Î” âŸ¶ Î“ â–¹ T) : f = âŸªf â‰« CwF.p, extHead fâŸ« := by
    have p : _ := ext_nat CwF.p f CwF.v
    rw [ext_id] at p
    aesop

  def termFromSlice {Î“ Î” : C} {T : Ty Î”}
    (f : Î“ âŸ¶ Î”)
    (sliceArr : (CategoryTheory.Over.mk f) âŸ¶ (CategoryTheory.Over.mk (CwF.p (T := T))))
    : Tm (Tâ¦ƒfâ¦„) :=
      castTm (extHead sliceArr.left) (by
    have pf := Over.w sliceArr
    simp_all)

  def termToSlice {Î“ Î” : C} {T : Ty Î”}
    (f : Î“ âŸ¶ Î”) (t : Tm (Tâ¦ƒfâ¦„))
    : ( (CategoryTheory.Over.mk f) âŸ¶ (CategoryTheory.Over.mk (CwF.p (T := T)))) := by
    fapply Over.homMk
    . simp_all
      exact âŸªf , tâŸ«
      --TODO simplify this
    . simp_all -- Looks to be a lean4 bug, see https://github.com/leanprover/lean4/issues/3257
      reduce
      simp

  theorem termToFromSlice {Î“ Î” : C} {T : Ty Î”}
    (f : Î“ âŸ¶ Î”)
    (sliceArr : (CategoryTheory.Over.mk f) âŸ¶ (CategoryTheory.Over.mk (CwF.p (T := T))))
    : termToSlice f (termFromSlice f sliceArr) = sliceArr := by
    apply Over.OverMorphism.ext
    simp [termToSlice, termFromSlice]
    apply (Î» x => Eq.trans x (Eq.symm (headTmEq _)))
    rw [ext_inj]
    fconstructor
    . symm
      apply Over.w sliceArr
    . simp_all

  theorem termFromToSlice {Î“ Î” : C} {T : Ty Î”}
    (f : Î“ âŸ¶ Î”) (t : Tm (Tâ¦ƒfâ¦„))
    : termFromSlice f (termToSlice f t) = t := by
      simp [termFromSlice, termToSlice, extHead]
